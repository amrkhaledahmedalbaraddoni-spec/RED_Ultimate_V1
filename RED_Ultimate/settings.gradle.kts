pluginManagement {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

rootProject.name = "RED-Ultimate"

// Canonical RED Android product. The legacy Signal fork remains in app/ as an
// extraction source only; it is deliberately outside the build graph.
include(":app")
project(":app").projectDir = file("red-app")

// One protocol shared by Android and the backend.
include(":shared-proto")

// Root QA tasks consume these tools as a composite build. Artifact-only Android builds may skip
// the heavy QA composite; the full CI image still builds and tests it through its normal stages.
val skipBuildLogic = providers.gradleProperty("RED_SKIP_BUILD_LOGIC").orNull?.toBooleanStrictOrNull() ?: false
if (!skipBuildLogic) includeBuild("build-logic")

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        maven {
            url = uri("$rootDir/local-maven")
            content { includeGroup("org.signal") }
            metadataSources { gradleMetadata() }
        }
        // libsignal-android is a large AAR. Use two HTTPS front doors for Maven Central;
        // strict SHA-256 verification still rejects any byte not pinned in metadata.
        maven {
            url = uri("https://maven-central.storage-download.googleapis.com/maven2")
            content { includeGroup("org.signal") }
        }
        maven {
            url = uri("https://repo1.maven.org/maven2")
            content { includeGroup("org.signal") }
        }
        mavenCentral()
    }
    versionCatalogs {
        create("benchmarkLibs") { from(files("gradle/benchmark-libs.versions.toml")) }
        create("testLibs") { from(files("gradle/test-libs.versions.toml")) }
        create("lintLibs") { from(files("gradle/lint-libs.versions.toml")) }
    }
}
